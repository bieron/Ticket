#!/usr/bin/env perl
use strict; use warnings;#{{{
use FindBin;
use lib "$FindBin::RealBin/../lib/perl5", "$FindBin::RealBin/../lib";
use 5.010;
use Getopt::Long qw/:config bundling/;
use Ticket qw/cfg ticket_out/;
#}}}

my %O = (
    field => [qw/key summary/],
);
my $rtfm = not GetOptions(
    'h|help+'       => \$O{help},
    'f|field=s@'    => \$O{field},
    'l|link|url'    => \$O{url},
);
if ($rtfm || ! @ARGV) {
    $O{help} = 1;
}
if ($O{help}) {
    require Pod::Usage;
    Pod::Usage::pod2usage({-verbose => $O{help}-1, -exitval => $rtfm});
}

my @tickets = Ticket::tracker->search(query => (join ' ', @ARGV), fields => $O{field});

if ($O{url}) {
    my $url = cfg('tracker_host');
    $_->{key} = $url .'browse/'. $_->{key} for @tickets;
}

ticket_out $_, @{$O{field}} for @tickets;
__END__
=pod

=head1 NAME

 jql - shell interface to Jira issue search

=head1 SYNOPSIS

 jql [-f FIELD...] [-l] JQL_QUERY

=head1 DESCRIPTION

 Searches for issues in Jira with JQL_QUERY.
 Results are displayed by key and summary, one per line.

 When connecting to Jira, uses credentials stored in ticket.conf

=head1 OPTIONS

 -h, --help
    Display this text. Ignores other options.

 -f, --field
    Specify what fields of tickets found to show.
    Fields displayed in order given.

 -l, --link, --url
    Display url to issue instead of issue key.

=head1 EXAMPLES

  jql -f summary -f assignee component="API" and status="In QA"

  jql assignee=$USER and status=open
    List of user's own assignments, by key and summary

=head1 AUTHOR

 Jan Biero≈Ñ, bieron@github.com

=cut
